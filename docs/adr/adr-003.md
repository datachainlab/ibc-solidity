# ADR-003: Channel Upgradable Module

## Changelog

- 2024-04-16: Initial draft

## Overview

With the introduction of [04-upgrade](https://github.com/cosmos/ibc/blob/main/spec/core/ics-004-channel-and-packet-semantics/UPGRADES.md) specification and [ADR-002](./adr-002.md), IBC Modules can now upgrade to a new version with non-backward-compatible changes while utilizing existing channels. This document defines how Channel Upgradable Modules ensure the upgrader's intended parameters are correctly applied during the upgrade process and how to complete the upgrade process by transition from `FLUSHING` to `FLUSHCOMPLETE`.

## Motivation

This ADR addresses two features that IBC Modules that support the channel upgrade should implement: **parameter validation** during upgrades and **transition control** for channel states.

- **Upgrade Parameter Validation**

    The ICS-04 upgrade introduces two primary methods for establishing agreement on upgrade parameters. The first method, referred to as *Non-Crossing Hello*, involves executing `ChanUpgradeInit` on chain A, where the proposed upgrade parameters are subsequently accepted by chain B. The second method, called *Crossing Hello*, involves both chain A and chain B executing `ChanUpgradeInit` and `ChanUpgradeTry`, ensuring that the upgrade parameters are mutually compatible before accepting the upgrade.

    In *Crossing Hello*, both chains verify each other's upgrade parameters, ensuring that the upgrade is executed with the intended values set by each upgrader. In contrast, *Non-Crossing Hello* defaults to adopting the parameters proposed by the counterparty, which does not inherently guarantee that the expected parameters are applied. To address this, the IBC Module must validate the counterparty chain's proposal within the `onChanUpgradeTry` function. This requires the IBC Module to store the expected upgrade parameters in advance. This ADR defines the functions required for setting the expected parameters and validating the counterparty's proposed values during upgrades.

- **Transition Control for FLUSHING to FLUSHCOMPLETE**

    In certain cases, explicit permission from the upgrader is required when transiting a channel from `FLUSHING` to `FLUSHCOMPLETE`. This requirement is defined in the [ADR-002](./adr-002.md#cantransitiontoflushcomplete). This ADR defines an interface that enables the upgrader to allow the IBC Module to transition to `FLUSHCOMPLETE`. Note that the upgrader must ensure that all inflight packets/acks have been received have been completed before allowing the transition.

## Specification

The following states, modifiers, functions, and module callback functions are implemented in the IBC Module.

### State

#### upgradeProposals

`UpgradeProposal` stores the upgrade parameters approved by the upgrader and the upgrade timeout (absolute). This is stored per channel. For the definition of `UpgradeFields`, refer to [ics-04](https://github.com/cosmos/ibc/blob/main/spec/core/ics-004-channel-and-packet-semantics/UPGRADES.md#upgradefields).

```solidity
/**
 * @dev Proposed upgrades for each channel
 */
mapping(string portId => mapping(string channelId => UpgradeProposal)) internal upgradeProposals;

/**
 * @dev Proposed upgrade fields
 * @param fields Upgrade fields
 * @param timeout Absolute timeout for the upgrade
 */
struct UpgradeProposal {
    UpgradeFields.Data fields;
    Timeout.Data timeout;
}
```

#### allowedTransactions

`AllowedTransition` contains the boolean value that allows the channel to transition to `FLUSHCOMPLETE`. This is stored per `upgradeSequence`.

```solidity
/**
 * @dev Allowed transition for the channel upgrade
 * @param flushComplete Whether the upgrade is allowed to transition to the flush complete state
 */
struct AllowedTransition {
    bool flushComplete;
}

/**
 * @dev Allowed transitions for each upgrade sequence
 */
mapping(string portId => mapping(string channelId => mapping(uint64 => AllowedTransition))) internal allowedTransitions;
```

### Modifiers

#### onlyAuthorizedUpgrader

Only the `AuthorizedUpgrader` should propose upgrade parameters or allow transitions to `FLUSHCOMPLETE`. The `onlyAuthorizedUpgrader` modifier verifies that `msg.sender` is an authorized upgrader.

```solidity
/**
 * @dev Throws if the sender is not an authorized upgrader
 * @param portId Port identifier
 * @param channelId Channel identifier
 */
modifier onlyAuthorizedUpgrader(string calldata portId, string calldata channelId) {
    if (!isAuthorizedUpgrader(portId, channelId, _msgSender())) {
        revert IBCChannelUpgradeAuthorizationUnauthorizedUpgrader();
    }
    _;
}
```

### Functions

#### proposeUpgrade

`proposeUpgrade` allows the upgrader to propose parameters for the next channel upgrade. Before executing `channelUpgradeInit` or `channelUpgradeTry`, the upgrader must call this function to set the `UpgradeProposal` in the IBC Module. The IBC Module must store the upgrade parameters along with the allowed timeout period. The upgrade parameters and timeout value may be re-proposed [MAY] if the channel's state is `OPEN`.

```solidity
function proposeUpgrade(
    string calldata portId,
    string calldata channelId,
    UpgradeFields.Data calldata upgradeFields,
    Timeout.Data calldata timeout
) public virtual override onlyAuthorizedUpgrader(portId, channelId) {
    if (timeout.height.revision_number == 0 && timeout.height.revision_height == 0 && timeout.timestamp == 0) {
        revert IBCChannelUpgradableModuleInvalidTimeout();
    }
    if (upgradeFields.ordering == Channel.Order.ORDER_NONE_UNSPECIFIED || upgradeFields.connection_hops.length == 0) {
        revert IBCChannelUpgradableModuleInvalidConnectionHops();
    }
    (Channel.Data memory channel, bool found) = IIBCHandler(ibcAddress()).getChannel(portId, channelId);
    if (!found) {
        revert IBCChannelUpgradableModuleChannelNotFound();
    }
    UpgradeProposal storage upgrade = upgradeProposals[portId][channelId];
    if (upgrade.fields.connection_hops.length != 0) {
        // Re-proposal is allowed as long as it does not transition to FLUSHING state yet
        if (channel.state != Channel.State.STATE_OPEN) {
            revert IBCChannelUpgradableModuleCannotOverwriteUpgrade();
        }
    }
    upgrade.fields = upgradeFields;
    upgrade.timeout = timeout;
}
```

#### removeUpgradeProposal

An `UpgradeProposal` is deleted in `onChanUpgradeOpen` when the subsequent upgrade handshake succeeds. However, if the handshake aborts, the proposal is not deleted. The upgrader can remove the proposal using `removeUpgradeProposal`, but only if the channel state is `OPEN`.

```solidity
function removeUpgradeProposal(string calldata portId, string calldata channelId) external onlyAuthorizedUpgrader(portId, channelId) {
    if (upgradeProposals[portId][channelId].fields.connection_hops.length == 0) {
        revert IBCChannelUpgradableModuleUpgradeNotFound();
    }
    IIBCHandler handler = IIBCHandler(ibcAddress());
    (, bool found) = handler.getChannelUpgrade(portId, channelId);
    if (found) {
        Channel.Data memory channel;
        (channel, found) = handler.getChannel(portId, channelId);
        if (!found) {
            revert IBCChannelUpgradableModuleChannelNotFound();
        }
        if (channel.state != Channel.State.STATE_OPEN) {
            revert IBCChannelUpgradableModuleCannotRemoveInProgressUpgrade();
        }
    }
    delete upgradeProposals[portId][channelId];
}
```

#### allowTransitionToFlushComplete

`allowTransitionToFlushComplete` enables the upgrader to allow a channel in the `FLUSHING` state to transition to `FLUSHCOMPLETE`. This function requires both channel information and upgrade sequence to prevent invalid transitions when the upgrade process is aborted and a new upgrade handshake is performed afterwards. The upgrader should ensure that all in-flight packets have been received by the counterparty chain and that all necessary acknowledgements are complete before calling this function. If the upgrader allows the transition to `FLUSHCOMPLETE` without flushing in-flight packets/acks and the upgrade is completed, they will never be received after the upgrade.

```solidity
function allowTransitionToFlushComplete(string calldata portId, string calldata channelId, uint64 upgradeSequence)
    public virtual override
    onlyAuthorizedUpgrader(portId, channelId)
{
    UpgradeProposal storage upgrade = upgradeProposals[portId][channelId];
    if (upgrade.fields.connection_hops.length == 0) {
        revert IBCChannelUpgradableModuleUpgradeNotFound();
    }
    (, bool found) = IIBCHandler(ibcAddress()).getChannelUpgrade(portId, channelId);
    if (!found) {
        revert IBCChannelUpgradableModuleUpgradeNotFound();
    }
    (Channel.Data memory channel,) = IIBCHandler(ibcAddress()).getChannel(portId, channelId);
    if (channel.state != Channel.State.STATE_FLUSHING) {
        revert IBCChannelUpgradableModuleChannelNotFlushingState(channel.state);
    }
    if (channel.upgrade_sequence != upgradeSequence) {
        revert IBCChannelUpgradableModuleSequenceMismatch(channel.upgrade_sequence);
    }
    allowedTransitions[portId][channelId][upgradeSequence].flushComplete = true;
}
```

### Module Callbacks

Module Callbacks are called by the IBC Core contract during the Upgrade Handshake. These interfaces generally comply with  the [ICS-04](https://github.com/cosmos/ibc/blob/main/spec/core/ics-004-channel-and-packet-semantics/UPGRADES.md), but with additional functions like `isAuthorizedUpgrader`, `getUpgradeTimeout`, and `canTransitionToFlushComplete`, as introduced in the [ADR-002](./adr-002.md).

#### isAuthorizedUpgrader

`isAuthorizedUpgrader` was introduced in the [ADR-002](./adr-002.md). the IBC Module must ensure that the `msgSender` is authorized to execute `ChanUpgradeInit`. The implementation detail depends on an IBC Module.

```solidity
function isAuthorizedUpgrader(string calldata portId, string calldata channelId, address msgSender)
    external
    view
    returns (bool);
```

#### getUpgradeTimeout

`getUpgradeTimeout` returns the value set by `proposeUpgrade`, as introduced in the [ADR-002](./adr-002.md).

```solidity
function getUpgradeTimeout(string calldata portId, string calldata channelId)
    public
    view
    virtual
    override
    returns (Timeout.Data memory)
{
    if (upgradeProposals[portId][channelId].fields.connection_hops.length == 0) {
        revert IBCChannelUpgradableModuleUpgradeNotFound();
    }
    return upgradeProposals[portId][channelId].timeout;
}
```

#### canTransitionToFlushComplete

`canTransitionToFlushComplete` returns the boolean value set by `allowTransitionToFlushComplete`, as introduced in the [ADR-002](./adr-002.md).

```solidity
function canTransitionToFlushComplete(
    string calldata portId,
    string calldata channelId,
    uint64 upgradeSequence,
    address
) public view virtual override returns (bool) {
    return allowedTransitions[portId][channelId][upgradeSequence].flushComplete;
}
```

#### onChanUpgradeInit

`onChanUpgradeInit` is a callback function from `ChanUpgradeInit`, initiated by the authorized upgrader. The IBC Module [SHOULD] ensure that the proposed upgrade matches the stored upgrade proposal within this callback.

```solidity
function onChanUpgradeInit(
    string calldata portId,
    string calldata channelId,
    uint64,
    UpgradeFields.Data calldata proposedUpgradeFields
) public view virtual override onlyIBC returns (string calldata version) {
    UpgradeProposal storage upgrade = upgradeProposals[portId][channelId];
    if (upgrade.fields.connection_hops.length == 0) {
        revert IBCChannelUpgradableModuleUpgradeNotFound();
    }
    if (!equals(upgrade.fields, proposedUpgradeFields)) {
        revert IBCChannelUpgradableModuleInvalidUpgrade();
    }
    return proposedUpgradeFields.version;
}
```

#### onChanUpgradeTry

`onChanUpgradeTry` is a callback function from `ChanUpgradeTry`, initiated by the relayer. The IBC Module [MUST] ensure that the proposed upgrade matches the stored proposal within this callback. In the *Non-Crossing Hello* case, the upgrade is executed based on the counterparty's proposed parameters, so the IBC Module must verify that they match the parameters proposed via `proposeUpgrade`.

```solidity
function onChanUpgradeTry(
    string calldata portId,
    string calldata channelId,
    uint64,
    UpgradeFields.Data calldata proposedUpgradeFields
) public view virtual override onlyIBC returns (string calldata version) {
    UpgradeProposal storage upgrade = upgradeProposals[portId][channelId];
    if (upgrade.fields.connection_hops.length == 0) {
        revert IBCChannelUpgradableModuleUpgradeNotFound();
    }
    if (!equals(upgrade.fields, proposedUpgradeFields)) {
        revert IBCChannelUpgradableModuleInvalidUpgrade();
    }
    return proposedUpgradeFields.version;
}
```

## Reference Implementation

- [IBCChannelUpgradableModule.sol](../../contracts/apps/commons/IBCChannelUpgradableModule.sol)
