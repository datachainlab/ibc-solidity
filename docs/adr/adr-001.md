# ADR-001: Reduce Gas Cost of UpdateClient

## Changelog

- 2024-01-17: Initial draft

## Status

Implemented

## Summary

This document describes a proposal to reduce the ibc-solidity UpdateClient gas cost. The current UpdateClient gas cost is high due to several inefficient and non-critical operations that are unnecessary after the handshake, such as proto-decode and updating client commitments. By offloading these operations from on-chain to off-chain, and by enabling the separation of state and commitment updates, we have achieved a reduction of approximately 86,000 gas in the MockClient, from 126,635 gas to 39,892 gas.

## Context

In the current version of ibc-solidity, the gas cost of the UpdateClient operation is inefficient. This can be attributed to the following three issues:

1. Cost of Computation and Storing ClientState/ConsensusState Commitment: Each time the UpdateClient operation is executed, the ClientState/ConsensusState commitment is calculated and stored. 
The storage cost is about 23k gas for two SSTORE operations. In addition, the computation costs include memory and arithmetic operations, as well as the cost of reading the state from the storage for the commitment calculation. The latter is proportional to the size of the Light Client state.
2. Cost of call/delegatecall: The UpdateClient message is submitted to the handler, which resolves the LC contract from the clientID. It then passes the message as an argument to the `updateClient` function of the LC contract and stores the returned commitment of the function. The delegatecall costs approximately 4.7k(2.1k+2.6k) gas per call. Due to contract size limitations, the handler consists of multiple logic contracts, so it takes two calls to reach an LC contract call. If an upgrade proxy pattern is applied to the handler, three calls are required.
3. Cost of proto-decoding: The relayer sends a proto-encoded client message to the update client. Therefore, the LC contract must decode the proto message, which causes significant gas costs.

## Solutions

In order to address these issues, the following three improvements are proposed:

1. Separation of commitment and state updates: Only update state in UpdateClient. Also, add a function to the handler to enable the update of the commitment corresponding to the state of the specified height. This reduces the cost of many UpdateClients.
2. Avoid UpdateClient via the handler: Update LC state directly instead. This reduces the cost of delegatecalls.
3. Removal of proto-decoding in transaction: Avoid decoding a proto message in a transaction and redefine struct as ethereum ABI.

To implement these in ibc-solidity, the following three points need to be resolved:

a. how to update the commitments in the host(i.e. handler) after updating LC state directly
b. how to determine the ethereum ABI format from the proto definition
c. how to know the function definition of the LC contract to which the message is sent

To resolve `a`, we added `updateClientCommitments` function to the handler which allows relayers to update the commitments corresponding to the LC state. Since the anyone can call directly `updateClient` in LC contract without the handler, it is required to call this function to set the commitment in the handler after the state is updated. However, in an established channel, there is almost no need to update the commitments every time per `updateClient`. An example implementation of yui-relayer, which does not update client commitments after a connection is established, can be found in the Appendix.

To resolve `b` and `c`, we added a new helper function `routeUpdateClient` to `ILightClient`. The function decodes the given proto-encoded message, converts it to eth-ABI message and returns it with the call information of the `updateClient` function. The function could be implemented off-chain, such as in a relayer, but considering the backwards compatibility with existing proto-encoded `updateClient` messages, we decide that it should be provided in LC contract. Note that a relayer does not need to use `routeUpdateClient`, assuming it knows the ABI of LC's UpdateClient function.

### updateClientCommitments

Add the `updateClientCommitments` function to the handler to generate the commitment of the updated state in `updateClient`.

```solidity
function updateClientCommitments(string calldata clientId, Height.Data[] memory heights) public {
    ILightClient lc = checkAndGetClient(clientId);
    bytes memory clientState;
    bytes memory consensusState;
    bool found;
    (clientState, found) = lc.getClientState(clientId);
    require(found, "client not found");
    commitments[IBCCommitment.clientStateCommitmentKey(clientId)] = keccak256(clientState);
    for (uint256 i = 0; i < heights.length; i++) {
        (consensusState, found) = lc.getConsensusState(clientId, heights[i]);
        require(found, "consensus state not found");
        bytes32 key = IBCCommitment.consensusStateCommitmentKey(
            clientId, heights[i].revision_number, heights[i].revision_height
        );
        bytes32 commitment = keccak256(consensusState);
        bytes32 prev = commitments[key];
        if (prev != bytes32(0) && commitment != prev) {
            // Revert if the new commitment is inconsistent with the previous one.
            // This case may indicate misbehavior of either the LightClient or the target chain.
            // Since the definition and specification of misbehavior are defined for each LightClient,
            // if a relayer detects this error, it is recommended to submit an evidence of misbehaviour to the LightClient accordingly.
            // (e.g., via the updateClient function).
            revert IBCClientInconsistentConsensusStateCommitment(key, commitment, prev);
        }
        commitments[key] = keccak256(consensusState);
    }
}
```

`clientId` is the identifier of the target light client instance, and `heights` is a list of heights for updating the commitment.

### routeUpdateClient

The definition of the `routeUpdateClient` function of the `ILightClient` interface is as follows.

```solidity
function routeUpdateClient(string calldata clientId, bytes calldata protoClientMessage) public pure returns (bytes4 selector, bytes memory args);
```
- `clientId` is the identifier of the target client
- `protoClientMessage` is a byte string of proto encoding of Any message composed of the proto encoding Value of the header given to the Light Client and the URL indicating its message type.

The `routeUpdateClient` performs the following operations:

- decodes `protoClientMessage` and encodes it to convert it to a solidity structure generated from the proto definition by solidity-protobuf
- returns the selector of the function that receives the structure and eth-abi encoded message

An implementation in MockClient is as follows:

```solidity
function routeUpdateClient(string calldata clientId, bytes calldata protoClientMessage) public pure override returns (bytes4, bytes memory) {
    Any.Data memory any = Any.decode(protoClientMessage);
    require(keccak256(abi.encodePacked(any.type_url)) == HEADER_TYPE_URL_HASH, "invalid header type");
    Header.Data memory header = Header.decode(any.value);
    return (this.updateClient.selector, abi.encode(clientId, header));
}

function updateClient(string calldata clientId, Header.Data calldata header) public returns (Height.Data[] memory heights) {
    require(statuses[clientId] == ClientStatus.Active, "client not active");
    require(header.height.revision_number == 0 && header.height.revision_height != 0, "invalid revision height");
    require(header.timestamp != 0, "invalid timestamp");
    if (header.height.gt(clientStates[clientId].latest_height)) {
        clientStates[clientId].latest_height = header.height;
    }
    consensusStates[clientId][header.height.toUint128()].timestamp = header.timestamp;
    heights = new Height.Data[](1);
    heights[0] = header.height;
    return heights;
}
```

In the above implementation, the `routeUpdateClient` function returns the selector of the public `updateClient` function implemented in the LC contract. Clients such as yui-relayer can send a transaction to execute `updateClient` with the two return values of `routeUpdateClient`.

## Security Concerns

### Constructing Tx using `routeUpdateClient`

If a relayer connects to an untrusted blockchain node and constructs a transaction using the tampered returned value of `routeUpdateClient`, an arbitrary contract call attack can be happened. Therefore, the relayer needs to register a LC contract address and function selectors in the allowed list and ensure that the returned value of  `routeUpdateClient` matches them. However, if the LC contract provides a relayer-trust update function, such a function should not be included within the allowed list. In that case, the relayer should be given the UpdateClient ABI and the relayer should construct an UpdateClient tx instead of using `routeUpdateClient`.

## Backward Compatibility

We can implement the handler's existing `updateClient(MsgUpdateClient)` as follows using `routeUpdateClient` and `updateClientCommitments`.

```solidity
function updateClient(MsgUpdateClient calldata msg_) external override {
    (address lc, bytes4 selector, bytes memory args) = routeUpdateClient(msg_);
    (bool success, bytes memory returndata) = lc.call(abi.encodePacked(selector, args));
    if (!success) {
        if (returndata.length > 0) {
            assembly {
                let returndata_size := mload(returndata)
                revert(add(32, returndata), returndata_size)
            }
        } else {
            revert("update client failed");
        }
    }
    Height.Data[] memory heights = abi.decode(returndata, (Height.Data[]));
    if (heights.length > 0) {
        updateClientCommitments(msg_.clientId, heights);
    }
}
```

## Appendix: Relayer implementation

Shows an implementation example in the ethereum module of yui-relayer https://github.com/datachainlab/ethereum-ibc-relay-chain. Note that yui-relayer relays between a "path" consisting of a single connection and channel.

In the function `SendMsgs`, which sends a transaction to ethereum, it can be determined that the commitment update  can be skipped if the connection state is confirmed to be `OPEN` by `QueryConnection`.

```go
func (c *Chain) SendMsgs(msgs []sdk.Msg) ([]core.MsgID, error) {
    ctx := context.Background()
    // if src's connection is OPEN, dst's connection is OPEN or TRYOPEN, so we can skip to update client commitments
    skipUpdateClientCommitment, err := c.checkConnectionOpened(ctx)
    if err != nil {
        return nil, err
    }
    var msgIDs []core.MsgID
    for i, msg := range msgs {
        var (
            tx  *gethtypes.Transaction
            err error
        )
        opts := c.TxOpts(ctx)
        switch msg := msg.(type) {
        case *clienttypes.MsgCreateClient:
            tx, err = c.TxCreateClient(opts, msg)
        case *clienttypes.MsgUpdateClient:
            tx, err = c.TxUpdateClient(opts, msg, skipUpdateClientCommitment)
        case *conntypes.MsgConnectionOpenInit:
            tx, err = c.TxConnectionOpenInit(opts, msg)
	  //... omitted
    }
}

func (c *Chain) checkConnectionOpened(ctx context.Context) (bool, error) {
    if c.connectionOpened {
        return true, nil
    }
    latestHeight, err := c.LatestHeight()
    if err != nil {
        return nil, err
    }
    connRes, err := c.QueryConnection(
        core.NewQueryContext(ctx, latestHeight)
    )
    if err != nil {
        return false, err
    }
    if connRes.Connection.State != conntypes.OPEN {
        return false, nil
    }
    c.connectionOpened = true
    return true, nil
}

func (c *Chain) TxUpdateClient(opts *bind.TransactOpts, msg *clienttypes.MsgUpdateClient, skipUpdateClientCommitment bool) (*gethtypes.Transaction, error) {
    clientMessageBytes, err := proto.Marshal(msg.ClientMessage)
    if err != nil {
        return nil, err
    }
    m := ibchandler.IIBCClientMsgUpdateClient{
        ClientId:      msg.ClientId,
        ClientMessage: clientMessageBytes,
    }
    if skipUpdateClientCommitment {
        lcAddr, fnID, args, err := c.ibcHandler.RouteUpdateClient(c.CallOpts(context.TODO(), 0), m)
        if err != nil {
            return err
        }
        // WARNING: if the relayer does not trust the blockchain node, it should check the `lcAddr` and `fnID` with the allowed list
        // For the details, see the Security Concerns section of ADR-001
        if c.checkAllowList(lcAddr, fnID) {
            calldata := append(fnID[:], args...)
            return bind.NewBoundContract(lcAddr, abi.ABI{}, c.client, c.client, c.client).RawTransact(opts, calldata)
        }
        // fallback to send a transaction to the handler
    }
    return c.ibcHandler.UpdateClient(opts, m)
}
```
